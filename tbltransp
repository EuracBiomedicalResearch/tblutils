#!/usr/bin/perl
# tbltransp: transpose a CSV file
# Copyright(c) 2008 EURAC, Institute of Generic Medicine
use strict;
use warnings;
use locale;
use Getopt::Std;

my %flags;
getopts("h", \%flags);
my $SEP = ($ENV{'TBLSEP'} || "\t");
my ($file) = @ARGV;
if(!$file || defined($flags{'h'}))
{
  print(STDERR qq{$0 [-h] file:
Transpose the contents of a CSV file. CSV files are TAB separated by default.
You can change the column separator by setting the TBLSEP environment variable.

  -h:	help summary
});
  exit(2);
}

# open the file
open(FD, $file) or die("cannot open $file\n");
my @res;

# read input
while(<FD>)
{
  s/[\r\n]+$//;
  my @cols = split($SEP, $_, -1);

  foreach(0 .. $#cols)
  {
    $res[$_][$. - 1] = $cols[$_];
  }
}

# output
foreach(@res)
{
  print(join($SEP, @$_) . "\n");
}
